## ***************************************************************
## Oxygen sensor heater feature
{if !defined(UEGO_NUMBER)
  !error "Missing UEGO_NUMBER."
}

!default SENSOR_OXYGEN_UEGO_HEATER_PID_KP 2.5
!default SENSOR_OXYGEN_UEGO_HEATER_PID_KI 0.02
!default SENSOR_OXYGEN_UEGO_HEATER_PID_KD 0.3
!default SENSOR_OXYGEN_UEGO_HEATER_PID_CLAMP_L -10
!default SENSOR_OXYGEN_UEGO_HEATER_PID_CLAMP_H 10
!default SENSOR_OXYGEN_UEGO_HEATER_PID_INT_L -10
!default SENSOR_OXYGEN_UEGO_HEATER_PID_INT_H 10

{create_item "UEGO_##UEGO_NUMBER##_heater_pid [%]"
  type : "PID Controller"
  thread_ID : OAL_THREAD_WITH_SLEEP_50
  clamp_flag : 1
  clamp_lower : SENSOR_OXYGEN_UEGO_HEATER_PID_CLAMP_L
  clamp_upper : SENSOR_OXYGEN_UEGO_HEATER_PID_CLAMP_H
  inputs : ["UEGO_##UEGO_NUMBER##_heater_UR_measured",       \
            "UEGO_##UEGO_NUMBER##_heater_UR_desired",        \
            "UEGO_##UEGO_NUMBER##_heater_pid_kp",            \
            "UEGO_##UEGO_NUMBER##_heater_pid_ki",            \
            "UEGO_##UEGO_NUMBER##_heater_pid_kd",            \
            "UEGO_##UEGO_NUMBER##_heater_using_pid [bool]",  \
            "UEGO_##UEGO_NUMBER##_heater_pid_int_hold",      \
            "UEGO_##UEGO_NUMBER##_heater_pid_int_l",         \
            "UEGO_##UEGO_NUMBER##_heater_pid_int_h",         \
            "UEGO_##UEGO_NUMBER##_heater_pid_error_output",  \
            "UEGO_##UEGO_NUMBER##_heater_pid_dt_output",     \
            "UEGO_##UEGO_NUMBER##_heater_pid_p_output",      \
            "UEGO_##UEGO_NUMBER##_heater_pid_i_output",      \
            "UEGO_##UEGO_NUMBER##_heater_pid_d_output",      \
            "UEGO_##UEGO_NUMBER##_heater_pid_deadband [%]"   \
           ]
}

!create_item "UEGO_##UEGO_NUMBER##_heater_pid_kp" [type="No Operation", initial_value=SENSOR_OXYGEN_UEGO_HEATER_PID_KP]
!create_item "UEGO_##UEGO_NUMBER##_heater_pid_ki" [type="No Operation", initial_value=SENSOR_OXYGEN_UEGO_HEATER_PID_KI]
!create_item "UEGO_##UEGO_NUMBER##_heater_pid_kd" [type="No Operation", initial_value=SENSOR_OXYGEN_UEGO_HEATER_PID_KD]
!create_item "UEGO_##UEGO_NUMBER##_heater_pid_int_hold" [type="No Operation"]
!create_item "UEGO_##UEGO_NUMBER##_heater_pid_int_l" [type="No Operation", initial_value=SENSOR_OXYGEN_UEGO_HEATER_PID_INT_L]
!create_item "UEGO_##UEGO_NUMBER##_heater_pid_int_h" [type="No Operation", initial_value=SENSOR_OXYGEN_UEGO_HEATER_PID_INT_H]
!create_item "UEGO_##UEGO_NUMBER##_heater_pid_error" [type="No Operation"]
!create_item "UEGO_##UEGO_NUMBER##_heater_pid_pcontrib" [type="No Operation"]
!create_item "UEGO_##UEGO_NUMBER##_heater_pid_icontrib" [type="No Operation"]
!create_item "UEGO_##UEGO_NUMBER##_heater_pid_dcontrib" [type="No Operation"]

!replace_item "UEGO_##UEGO_NUMBER##_heater_pid_error_output" [type="Constant", value="0"]
!replace_item "UEGO_##UEGO_NUMBER##_heater_pid_dt_output" [type="Constant", value="0"]
!replace_item "UEGO_##UEGO_NUMBER##_heater_pid_p_output" [type="Constant", value="0"]
!replace_item "UEGO_##UEGO_NUMBER##_heater_pid_i_output" [type="Constant", value="0"]
!replace_item "UEGO_##UEGO_NUMBER##_heater_pid_d_output" [type="Constant", value="0"]

!create_item "UEGO_##UEGO_NUMBER##_heater_pid_deadband [%]" [type="No Operation", initial_value="0.1"]

!notebook new_note "UEGO heater ##UEGO_NUMBER## pid" [vendor="Enginelab, Inc.", item_wildcard="UEGO_##UEGO_NUMBER##_heater_pid", message=" The UEGO heater control submodel."]
#****************************************************************************************************************************
## This will create the enable for the PID duty generation for heating the UEGO sensor.
!create_item "UEGO_##UEGO_NUMBER##_heater_pid_enable_noop [bool]" [type="No Operation", initial_value="1"]

{create_item "UEGO_##UEGO_NUMBER##_heater_using_pid [bool]"
  type : "If-Else Condition"
  thread_ID : OAL_THREAD_WITH_SLEEP_200
  inputs : ["UEGO_##UEGO_NUMBER##_cj125v2_UR_measured [V]",       \
            "UEGO_##UEGO_NUMBER##_cj125v2_UR_cal [V]",            \
            "UEGO_##UEGO_NUMBER##_heater_pid_enable_noop [bool]", \
            "UEGO_##UEGO_NUMBER##_cj125v2_reset"                  \
           ]
  expressions : ["(i0-i1<1.0)&&i2&&!i3"]
}

!notebook new_note "UEGO_##UEGO_NUMBER##_heater_using_pid [bool]" [vendor="Enginelab, Inc.", message=" This will be the enable for the item \"UEGO_0_heater_pid [%]\" \n."]
#****************************************************************************************************************************

#****************************************************************************************************************************
!default SENSOR_OXYGEN_UEGO_HEATER_PID_DUTY_CLAMP_L 0
!default SENSOR_OXYGEN_UEGO_HEATER_PID_DUTY_CLAMP_H 60

{create_item "UEGO_##UEGO_NUMBER##_heater_pid_duty [%]" 
  type : "Math Calculation"
  thread_ID : OAL_THREAD_WITH_SLEEP_200
  initial_value : 50
  clamp_flag : 1
  clamp_lower : SENSOR_OXYGEN_UEGO_HEATER_PID_DUTY_CLAMP_L
  clamp_upper : SENSOR_OXYGEN_UEGO_HEATER_PID_DUTY_CLAMP_H
  inputs : ["UEGO_##UEGO_NUMBER##_heater_pid [%]"]
  expression : "self-i0"
}
!notebook new_note "UEGO_##UEGO_NUMBER##_heater_pid_duty [%]" [vendor="Enginelab, Inc.", message=" The contribution to the heater duty cycle based on the PID."]
#****************************************************************************************************************************

#****************************************************************************************************************************
!default SENSOR_OXYGEN_UEGO_RAMP_LOW 20
!default SENSOR_OXYGEN_UEGO_RAMP_HIGH 60
!default SENSOR_OXYGEN_UEGO_RAMP_TIME 30000

{create_item "UEGO_##UEGO_NUMBER##_heater_ramp_duty [%]"
  type : "Timer"
  thread_ID : OAL_THREAD_WITH_SLEEP_200
  inputs : ["uego_ramp_timer_low", \
            "uego_ramp_timer_time", \
            "UEGO_##UEGO_NUMBER##_heater_ramp_reset [bool]", \
            "uego_ramp_timer_high" \
           ]
  mode : "Linear Transition Over Time reset=start"
}

!replace_item "uego_ramp_timer_low" [type="Constant", value=SENSOR_OXYGEN_UEGO_RAMP_LOW]
!replace_item "uego_ramp_timer_high" [type="Constant", value=SENSOR_OXYGEN_UEGO_RAMP_HIGH]
!replace_item "uego_ramp_timer_time" [type="Constant", value=SENSOR_OXYGEN_UEGO_RAMP_TIME]
!notebook new_note "UEGO_##UEGO_NUMBER##_heater_ramp_duty [%]" [vendor="Enginelab, Inc.", message=" The contribution to the heater duty cycle based on the Ramp timer."]

{create_item "UEGO_##UEGO_NUMBER##_heater_ramp_reset [bool]"
  type : "If-Else Condition"
  thread_ID : OAL_THREAD_WITH_SLEEP_200
  inputs : ["UEGO_##UEGO_NUMBER##_heater_using_pid [bool]", \
            "UEGO_##UEGO_NUMBER##_cj125v2_reset" \
           ]
  expressions : "i0||i1"
}
!notebook new_note "UEGO_##UEGO_NUMBER##_heater_ramp_reset [bool]" [vendor="Enginelab, Inc.", message=" The reset for the heater Ramp timer."]
#****************************************************************************************************************************

#****************************************************************************************************************************
!create_item "UEGO_##UEGO_NUMBER##_heater_enable_noop [bool]" [type="No Operation", initial_value="1"]
#****************************************************************************************************************************
{create_item "UEGO_##UEGO_NUMBER##_heater_duty_final [%]"
  type : "Math Calculation"
  thread_ID : OAL_THREAD_WITH_SLEEP_100
  inputs : ["UEGO_##UEGO_NUMBER##_heater_pid_duty [%]", \
            "UEGO_##UEGO_NUMBER##_heater_ramp_duty [%]", \
            "UEGO_##UEGO_NUMBER##_heater_using_pid [bool]", \
            "UEGO_##UEGO_NUMBER##_heater_enable_noop [bool]", \
            "UEGO_##UEGO_NUMBER##_cj125v2_reset" \
           ]
  expression : "?(i2,i0,i1)*i3*!i4"
}
!notebook new_note "UEGO_##UEGO_NUMBER##_heater_duty_final [%]" [vendor="Enginelab, Inc.", message=" This item will choose the duty based on using the PID or the Ramping time."]
##****************************************************************************************************************************

##****************************************************************************************************************************
{create_item "UEGO_##UEGO_NUMBER##_heater_HW"
  type : "Hardware Placeholder"
  thread_ID : OAL_THREAD_WITH_SLEEP_100
  inputs : ["UEGO_##UEGO_NUMBER##_heater_frequency [Hz]", \
            "UEGO_##UEGO_NUMBER##_heater_duty_final [%]"  \
           ]
}
!replace_item "UEGO_##UEGO_NUMBER##_heater_frequency [Hz]" [type="Constant", value="30"]
##****************************************************************************************************************************

!notebook new_note "UEGO heater ##UEGO_NUMBER## submodel" [vendor="Enginelab, Inc.", item_wildcard="UEGO_##UEGO_NUMBER##_heater", item_or_group="UEGO heater ##UEGO_NUMBER## pid", message=" The UEGO heater control submodel."]

