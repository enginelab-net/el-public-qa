## ***************************************************************
## Oxygen sensor heater feature

{if defined(SENSOR_OXYGEN_UEGO_HEATER_PID_KP)
!else
!set SENSOR_OXYGEN_UEGO_HEATER_PID_KP 2.5
}
{if defined(SENSOR_OXYGEN_UEGO_HEATER_PID_KI)
!else
!set SENSOR_OXYGEN_UEGO_HEATER_PID_KI 0.02
}
{if defined(SENSOR_OXYGEN_UEGO_HEATER_PID_KD)
!else
!set SENSOR_OXYGEN_UEGO_HEATER_PID_KD 0.3
}

{if defined(SENSOR_OXYGEN_UEGO_HEATER_PID_CLAMP_L)
!else
!set SENSOR_OXYGEN_UEGO_HEATER_PID_CLAMP_L -10
}
{if defined(SENSOR_OXYGEN_UEGO_HEATER_PID_CLAMP_H)
!else
!set SENSOR_OXYGEN_UEGO_HEATER_PID_CLAMP_H 10
}

{if defined(SENSOR_OXYGEN_UEGO_HEATER_PID_INT_L)
!else
!set SENSOR_OXYGEN_UEGO_HEATER_PID_INT_L -10
}
{if defined(SENSOR_OXYGEN_UEGO_HEATER_PID_INT_H)
!else
!set SENSOR_OXYGEN_UEGO_HEATER_PID_INT_H 10
}

!create_item "UEGO_0_heater_pid [%]" [type="PID Controller", thread_ID=OAL_THREAD_WITH_SLEEP_50]
!change_item "UEGO_0_heater_pid [%]" [clamp_flag="1", clamp_lower=SENSOR_OXYGEN_UEGO_HEATER_PID_CLAMP_L, clamp_upper=SENSOR_OXYGEN_UEGO_HEATER_PID_CLAMP_H]
!change_item "UEGO_0_heater_pid [%]" [add_input="UEGO_0_heater_UR_measured"]
!change_item "UEGO_0_heater_pid [%]" [add_input="UEGO_0_heater_UR_desired"]
!change_item "UEGO_0_heater_pid [%]" [add_input="UEGO_0_heater_pid_kp"]
!change_item "UEGO_0_heater_pid [%]" [add_input="UEGO_0_heater_pid_ki"]
!change_item "UEGO_0_heater_pid [%]" [add_input="UEGO_0_heater_pid_kd"]
!change_item "UEGO_0_heater_pid [%]" [add_input="UEGO_0_heater_using_pid [bool]"]
!change_item "UEGO_0_heater_pid [%]" [add_input="UEGO_0_heater_pid_int_hold"]
!change_item "UEGO_0_heater_pid [%]" [add_input="UEGO_0_heater_pid_int_l"]
!change_item "UEGO_0_heater_pid [%]" [add_input="UEGO_0_heater_pid_int_h"]

!create_item "UEGO_0_heater_pid_kp" [type="No Operation", initial_value=SENSOR_OXYGEN_UEGO_HEATER_PID_KP]
!create_item "UEGO_0_heater_pid_ki" [type="No Operation", initial_value=SENSOR_OXYGEN_UEGO_HEATER_PID_KI]
!create_item "UEGO_0_heater_pid_kd" [type="No Operation", initial_value=SENSOR_OXYGEN_UEGO_HEATER_PID_KD]
!create_item "UEGO_0_heater_pid_int_hold" [type="No Operation"]
!create_item "UEGO_0_heater_pid_int_l" [type="No Operation", initial_value=SENSOR_OXYGEN_UEGO_HEATER_PID_INT_L]
!create_item "UEGO_0_heater_pid_int_h" [type="No Operation", initial_value=SENSOR_OXYGEN_UEGO_HEATER_PID_INT_H]
!create_item "UEGO_0_heater_pid_error" [type="No Operation"]
!create_item "UEGO_0_heater_pid_pcontrib" [type="No Operation"]
!create_item "UEGO_0_heater_pid_icontrib" [type="No Operation"]
!create_item "UEGO_0_heater_pid_dcontrib" [type="No Operation"]

!notebook new_note "UEGO heater pid" [vendor="Enginelab, Inc.", item_wildcard="UEGO_0_heater_pid", message=" The UEGO heater control submodel."]
#****************************************************************************************************************************
## This will create the enable for the PID duty generation for heating the UEGO sensor.
!create_item "UEGO_0_heater_pid_enable_noop [bool]" [type="No Operation", initial_value="1"]

!create_item "UEGO_0_heater_using_pid [bool]" [type="If-Else Condition", thread_ID=OAL_THREAD_WITH_SLEEP_200]
!change_item "UEGO_0_heater_using_pid [bool]" [add_input="UEGO_0_cj125v2_UR_measured [V]"]
!change_item "UEGO_0_heater_using_pid [bool]" [add_input="UEGO_0_cj125v2_UR_cal [V]"]
!change_item "UEGO_0_heater_using_pid [bool]" [add_input="UEGO_0_heater_pid_enable_noop [bool]"]
!change_item "UEGO_0_heater_using_pid [bool]" [if_else_clear_expressions, if_else_add_expression="(i0-i1<1.0)&&i2"]

!notebook new_note "UEGO_0_heater_using_pid [bool]" [vendor="Enginelab, Inc.", message=" This will be the enable for the item \"UEGO_0_heater_pid [%]\" \n."]
#****************************************************************************************************************************

#****************************************************************************************************************************
{if defined(SENSOR_OXYGEN_UEGO_HEATER_PID_DUTY_CLAMP_L)
!else
!set SENSOR_OXYGEN_UEGO_HEATER_PID_DUTY_CLAMP_L 0
}
{if defined(SENSOR_OXYGEN_UEGO_HEATER_PID_DUTY_CLAMP_H)
!else
!set SENSOR_OXYGEN_UEGO_HEATER_PID_DUTY_CLAMP_H 60
}

!create_item "UEGO_0_heater_pid_duty [%]" [type="Math Calculation", thread_ID=OAL_THREAD_WITH_SLEEP_50, initial_value="30"]
!change_item "UEGO_0_heater_pid_duty [%]" [add_input="UEGO_0_heater_pid [%]"]
!change_item "UEGO_0_heater_pid_duty [%]" [math_expression="self-i0"]
!change_item "UEGO_0_heater_pid_duty [%]" [clamp_flag="1", clamp_lower=SENSOR_OXYGEN_UEGO_HEATER_PID_DUTY_CLAMP_L, clamp_upper=SENSOR_OXYGEN_UEGO_HEATER_PID_DUTY_CLAMP_H]
!change_item "UEGO_0_heater_pid_duty [%]" [thread_ID=OAL_THREAD_WITH_SLEEP_200]
!notebook new_note "UEGO_0_heater_pid_duty [%]" [vendor="Enginelab, Inc.", message=" The contribution to the heater duty cycle based on the PID."]
#****************************************************************************************************************************

#****************************************************************************************************************************
{if defined(SENSOR_OXYGEN_UEGO_RAMP_LOW)
!else
!set SENSOR_OXYGEN_UEGO_RAMP_LOW 20
}
{if defined(SENSOR_OXYGEN_UEGO_RAMP_HIGH)
!else
!set SENSOR_OXYGEN_UEGO_RAMP_HIGH 60
}
{if defined(SENSOR_OXYGEN_UEGO_RAMP_TIME)
!else
!set SENSOR_OXYGEN_UEGO_RAMP_TIME 30000
}
!create_item "UEGO_0_heater_ramp_duty [%]" [type="Timer", thread_ID=OAL_THREAD_WITH_SLEEP_200]
!change_item "UEGO_0_heater_ramp_duty [%]" [delete_inputs]
!change_item "UEGO_0_heater_ramp_duty [%]" [add_input="uego_ramp_timer_low"]
!change_item "UEGO_0_heater_ramp_duty [%]" [add_input="uego_ramp_timer_time"]
!change_item "UEGO_0_heater_ramp_duty [%]" [add_input="UEGO_0_heater_using_pid [bool]"]
!change_item "UEGO_0_heater_ramp_duty [%]" [add_input="uego_ramp_timer_high"]
!change_item "UEGO_0_heater_ramp_duty [%]" [timer_mode="Linear Transition Over Time reset=start"]
!replace_item uego_ramp_timer_low [type="Constant", constant_value=SENSOR_OXYGEN_UEGO_RAMP_LOW]
!replace_item uego_ramp_timer_high [type="Constant", constant_value=SENSOR_OXYGEN_UEGO_RAMP_HIGH]
!replace_item uego_ramp_timer_time [type="Constant", constant_value=SENSOR_OXYGEN_UEGO_RAMP_TIME]
!notebook new_note "UEGO_0_heater_ramp_duty [%]" [vendor="Enginelab, Inc.", message=" The contribution to the heater duty cycle based on the Ramp timer."]
#****************************************************************************************************************************

#****************************************************************************************************************************
!create_item "UEGO_0_heater_enable_noop [bool]" [type="No Operation", initial_value="1"]
#****************************************************************************************************************************
!create_item "UEGO_0_heater_duty_final [%]" [type="Math Calculation", thread_ID=OAL_THREAD_WITH_SLEEP_100]
!change_item "UEGO_0_heater_duty_final [%]" [add_input="UEGO_0_heater_pid_duty [%]"]
!change_item "UEGO_0_heater_duty_final [%]" [add_input="UEGO_0_heater_ramp_duty [%]"]
!change_item "UEGO_0_heater_duty_final [%]" [add_input="UEGO_0_heater_using_pid [bool]"]
!change_item "UEGO_0_heater_duty_final [%]" [add_input="UEGO_0_heater_enable_noop [bool]"]
!change_item "UEGO_0_heater_duty_final [%]" [math_expression="?(i2,i0,i1)*i3"]
!notebook new_note "UEGO_0_heater_duty_final [%]" [vendor="Enginelab, Inc.", message=" This item will choose the duty based on using the PID or the Ramping time."]
##****************************************************************************************************************************

##****************************************************************************************************************************
!create_item "UEGO_0_heater_HW" [type="Hardware Placeholder", thread_ID=OAL_THREAD_WITH_SLEEP_100]
!change_item "UEGO_0_heater_HW" [add_input="UEGO_0_heater_frequency [Hz]"]
!change_item "UEGO_0_heater_HW" [add_input="UEGO_0_heater_duty_final [%]"]
!replace_item "UEGO_0_heater_frequency [Hz]" [type="Constant", constant_value="30"]
##****************************************************************************************************************************

!notebook new_note "UEGO heater submodel" [vendor="Enginelab, Inc.", item_wildcard="UEGO_0_heater", item_or_group="UEGO heater pid", message=" The UEGO heater control submodel."]
